{smcl}
{right:Updated time: Aug 17, 2020}
{* -----------------------------title------------------------------------ *}{...}
{p 0 19 2}
{bf:[W-11] tabstat2} {hline 2} output the results of the tabstat command to Stata interface, Word and LaTeX. The source code can be gained in {browse "https://github.com/Meiting-Wang/tabstat2":github}.


{* -----------------------------Syntax------------------------------------ *}{...}
{title:Syntax}

{p 8 8 2}
{cmd:tabstat2} {varlist} {ifin} {weight} [{it:{help using:using filename}}] [, {it:{help tabstat2##Options:options}}]

{p 4 4 2}
where the subcommands can be :

{synoptset 20}{...}
{synopthdr:subcommand}
{synoptline}
{synopt :{it:{help varlist}}}Numeric variables required{p_end}
{synopt :{it:{help weight}}}Can choose one of {bf:aweight} and {bf:fweight}, empty as the default{p_end}
{synopt :{it:{help using}}}Output the result to Word with .rtf file or LaTeX with .tex file{p_end}
{synoptline}


{* -----------------------------Contents------------------------------------ *}{...}
{title:Contents}

{p 4 4 2}
{help tabstat2##Description:Description}{break}
{help tabstat2##Options:Options}{break}
{help tabstat2##Examples:Examples}{break}
{help tabstat2##Author:Author}{break}
{help tabstat2##Also_see:Also see}{break}


{* -----------------------------Description------------------------------------ *}{...}
{marker Description}{title:Description}

{p 4 4 2}
{bf:tabstat2}, based on {help tabstat} and {help esttab}, can output the results of the {help tabstat} command to Stata interface, Word and LaTeX, but this command is slightly different from {help table}, and you can explore it by yourself. It is worth noting that this command can only be used in version 16 or later.

{p 4 4 2}
You can also append the output result from {bf:tabstat2} to a existed word or LaTeX document,
which is more likely to be generated by {help wmtsum}, {help wmttest}, {help wmtcorr}, {help wmtreg}, {help wmtmat}, {help table2} and {help esttab}.


{* -----------------------------Options------------------------------------ *}{...}
{marker Options}{title:Options}

{synoptset 23 tabbed}{...}
{synopthdr}
{synoptline}
{syntab:For all}
{synopt :{opth by:(varname)}}A categorical variable required{p_end}
{synopt :{opth s:tatistics(strings)}}Specify the statistics to be calculated, and you can also set the format or label of the specific statistics like {bf:mean(}{opth f:mt(esttab##fmt:fmt)} {opth l:abel(strings:string)}{bf:)}. All possible statistics are {bf:count}, {bf:mean}, {bf:sd}, {bf:min}, {bf:max}, {bf:range}, {bf:variance}, {bf:sum}, {bf:p1}, {bf:p5}, {bf:p10}, {bf:p25}, {bf:p50}, {bf:p75}, {bf:p90}, {bf:p95}, {bf:p99}, {bf:iqr}, {bf:cv}, {bf:semean}, {bf:skewness}, {bf:kurtosis}. The meaning of these statistics can be viewed in {help tabstat}{p_end}
{synopt :{opt list:wise}}Use listwise deletion to handle missing values{p_end}
{synopt :{opt not:otal}}Not report the total sector{p_end}
{synopt :{opth ti:tle(strings:string)}}Set the title for the table{p_end}
{synopt :{opt replace}}Replace a file if it already exists{p_end}
{synopt :{opt append}}Append the result to a already existed file{p_end}
{synopt :{opth eql:abels(strings)}}Customize row equation names. {bf:none} means that row equation names are not reported{p_end}
{synopt :{opth varl:abels(strings:matchlist)}}Customize row variable names. {bf:none} means that row variable names are not reported{p_end}
{synopt :{opth coll:abels(strings)}}Customize column names. {bf:none} means that column names are not reported{p_end}
{synopt :{opth varw:idth(numlist:number)}}Customize the width of the first column of the table{p_end}
{synopt :{opth modelw:idth(numlist)}}Customize the width of the second and subsequent columns of the table{p_end}
{synopt :{opt compress}}Compress horizontal spacing for the table{p_end}

{syntab:For LaTeX only}
{synopt :{opth a:lignment(strings:string)}}Format the table columns in LaTeX, but it will not have influence on the Stata interface. Only {bf:math} or {bf:dot} can be included. {bf:math} set the table column format to math center alignment and {bf:dot} set the table column format to decimal point alignment, {bf:math} as the default{p_end}
{synopt :{opth page:(strings:string)}}Set the extra package for the LaTeX. Don't need to care about the package of booktabs, array and dcolumn, because option {bf:alignment} will deal with it automatically{p_end}
{synopt :{opth width:(strings:string)}}Set the width of table in LaTeX. For instance, set the width of the table to the width of the type page by {bf:\textwidth}{p_end}
{synoptline}


{* -----------------------------Examples------------------------------------ *}{...}
{marker Examples}{title:Examples}

{p 4 4 2}Setup{p_end}
{p 8 8 2}. {stata sysuse auto.dta, clear}{p_end}

{p 4 4 2}For all{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, listwise}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, s(count mean min max)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, s(count(label(n)) mean(fmt(2)) min(fmt(2)) max(fmt(2)))}{p_end}

{p 8 8 2}. {stata tabstat2 price weight mpg rep78, by(foreign)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, by(foreign) nototal}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, by(foreign) eql(domestic foreign Total)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, by(foreign) varl(price price_plus mpg mpg_plus)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, by(foreign) coll(col1 col2 col3 col4 col5)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, by(foreign) compress}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, by(foreign) compress varw(20)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, by(foreign) compress modelw(12)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78, by(foreign) ti(This is a title)}{p_end}

{p 4 4 2}For Word{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78 using Myfile.rtf, replace by(foreign)}{p_end}

{p 4 4 2}For LaTeX{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78 using Myfile.tex, replace by(foreign) ti(This is a title)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78 using Myfile.tex, replace by(foreign) ti(This is a title) a(dot)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78 using Myfile.tex, replace by(foreign) ti(This is a title) page(amsmath)}{p_end}
{p 8 8 2}. {stata tabstat2 price weight mpg rep78 using Myfile.tex, replace by(foreign) ti(This is a title) width(\textwidth)}{p_end}


{* -----------------------------Author------------------------------------ *}{...}
{marker Author}{title:Author}

{p 4 4 2}
Meiting Wang{break}
Institute for Economic and Social Research, Jinan University{break}
Guangzhou, China{break}
wangmeiting92@gmail.com


{* -----------------------------Also see------------------------------------ *}{...}
{marker Also_see}{title:Also see}

{space 4}{help tabstat}
{space 4}{help wmtsum}(already installed)  {col 40}{stata github install Meiting-Wang/wmtsum:install wmtsum}(to install)
{space 4}{help wmttest}(already installed) {col 40}{stata github install Meiting-Wang/wmttest:install wmttest}(to install)
{space 4}{help wmtcorr}(already installed) {col 40}{stata github install Meiting-Wang/wmtcorr:install wmtcorr}(to install)
{space 4}{help wmtreg}(already installed)  {col 40}{stata github install Meiting-Wang/wmtreg:install wmtreg}(to install)
{space 4}{help wmtmat}(already installed)  {col 40}{stata github install Meiting-Wang/wmtmat:install wmtmat}(to install)
{space 4}{help table2}(already installed)  {col 40}{stata github install Meiting-Wang/table2:install table2}(to install)
{space 4}{help esttab}(already installed)  {col 40}{stata ssc install estout:install esttab}(to install)

